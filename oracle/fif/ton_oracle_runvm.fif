// TON TVM oracle runner (via `fift` / `runvmx`).
//
// Usage:
//   /Users/daniil/Coding/ton/build/crypto/fift -I/Users/daniil/Coding/ton/crypto/fift/lib -s oracle/fif/ton_oracle_runvm.fif \
//     <code_boc_hex> [GAS <gas_limit>] [LIB <lib_collection_boc_hex>] [stack_args...]
//
// `code_boc_hex` is a hex-encoded BOC of a *code cell* (no 0x prefix).
//
// `stack_args` are initial VM stack values, in left-to-right order (bottom -> top):
//   I   -> push int 0
//   N   -> push null
//   C   -> push empty cell
//   S   -> push empty slice
//   B   -> push empty builder
//   T   -> push empty tuple (nil)
//   K   -> push quit continuation (exit_code=0)
//   CB:<boc_hex> -> push Cell from hex-encoded BOC
//   SB:<boc_hex> -> push Slice from hex-encoded BOC
//   BB:<boc_hex> -> push Builder with contents from hex-encoded BOC (cell bits+refs)
//   <n> -> push integer parsed from decimal string (e.g. "-3", "10")
//
// Output is produced by `oracle-run` from `oracle/fif/ton_oracle_runvm_lib.fif`:
//   EXIT  <exit_code>
//   GAS   <gas_used>
//   C4    <hash256>
//   C5    <hash256>
//   DEPTH <stack_depth>
//   STACK <idx_from_top> <val>

"oracle/fif/ton_oracle_runvm_lib.fif" include

variable argi

// Optionally set VM library collections for XLOAD{Q}.
// Usage extension:
//   <code_boc_hex> LIB <lib_collection_boc_hex> [stack_args...]
// If omitted, libraries are cleared (empty).
null vmlibs !
2 argi !

// Optionally set runvmx gas limit (defaults to 1_000_000 in the included lib).
$# argi @ >= {
  argi @ $() "GAS" $= {
    $# argi @ 1+ < { abort"missing GAS argument: expected <gas_limit> after GAS" } if
    argi @ 1+ $() parse-int oracle_gas_limit !
    argi @ 2 + argi !
  } { } cond
} { } cond

// Optionally set VM library collections for XLOAD{Q}.
// Usage extension:
//   <code_boc_hex> [GAS <gas_limit>] LIB <lib_collection_boc_hex> [stack_args...]
$# argi @ >= {
  argi @ $() "LIB" $= {
    $# argi @ 1+ < { abort"missing LIB argument: expected <lib_collection_boc_hex> after LIB" } if
    argi @ 1+ $() x>B B>boc vmlibs !
    argi @ 2 + argi !
  } { } cond
} { } cond

// Push initial stack args from $argi..$#
$# argi @ - 1+ {
  argi @ $() push-token
  argi @ 1+ argi !
} swap times

// Run and print
$1 oracle-run

bye
